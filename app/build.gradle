apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.example.volley"
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all{
            jacoco{ includeNoLocationClasses = true}
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Volley
    implementation 'com.android.volley:volley:1.1.1'

    // OkHttp
    implementation 'com.squareup.okhttp:okhttp:2.0.0'
    implementation 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    implementation 'com.squareup.okio:okio:2.8.0'
}

jacoco {
    toolVersion = "0.8.5"
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
//        html.destination file("build/reports/jacoco/jacocoTestReport/html/jacoco-resources")
    }
    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)

    sourceDirectories.from = files('src/main/java/com/example/employeedemo')
    classDirectories.from = files([debugTree])
    executionData.from = files('build/jacoco/testDebugUnitTest.exec')
}

/*
 * 1. Terminal: ./gradlew createDebugCoverageReport
 * 2. Open Project/app/build/reports/coverage/debug/index.html in Web
 */